V  = a e o ə
Vs = á é ó
cV = m n ŋ ŋʷ r l j w
vV = ũ ṅ ṅ ũ  ṙ ḷ i u
VV = {V} {vV}

P = p  b  mb  m
T = t  d  nd  n
K = k  g  ŋg  ŋ
W = kʷ gʷ ŋgʷ
H = ʔ h ɦ
S = 0 s z
R = r l j w ŋʷ
C = {P} {T} {K} {W} {H} {S} {R}
CC = {P} {T} {K} {W} {H} {S}

M  = mb nd ŋg
N  = m  n  ŋ  m n ŋ
Nt = p  t  k  b d g

// change to stress accent marking
{0:Vs} > ˈ{0:V}

// ABLAUT!
// Pretonic vowel matches tonic vowel
{V} > {0:V} / _{C}+ˈ{0:V}
// Propretonic vowels partially ablaut towards tonic:
// *a matches tonic
// *e matches *e or *a, but becomes *ə if tonic is *o
// *o matches *o or *a, but becomes *ə if tonic is *e
// *ə always stays *ə
Va = a a a ə
Ve = e e ə ə
Vo = o ə o ə
{0:V} > {0:Va} / _({C}+{V})+{C}+ˈa
{0:V} > {0:Ve} / _({C}+{V})+{C}+ˈe
{0:V} > {0:Vo} / _({C}+{V})+{C}+ˈo
// All posttonic vowels reduce to *ə
{V} > ə / ˈ({V}{C}+)+_

// syllabic consonants
ə{0:cV} > {0:vV} ! _ˈ?{VV}
{0:cV}ə > {0:vV} ! {VV}_

// labiovelars (except ŋʷ) become plain labials
{0:W} > {0:P}
// consonants (except liquids) disappear before prenasals
{CC} > 0 / _{M}
// glottals disappear between consonants or before initial consonants or after
// final consonants
{H} > 0 / #|{C}_{C}|#

// ŋʷ becomes a homoörganic nasal + w following a stop
ŋʷ > {0:N}w / {0:Nt}_
// ŋʷ becomes w following a nasal
ŋʷ > w / {N}_
// ŋʷ becomes w̃ otherwise
ŋʷ > w̃

// nasals disappear before nasals
{N} > 0 / _{N}
// nasals assimilate to a following stop
{N} > {0:N} / _{0:Nt}
// nasals assimilate to a preceding stop
{N} > {0:N} / {0:Nt}_

ˈ{0:V} > {0:Vs}
