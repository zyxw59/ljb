V = i e ɛ ə a u o ɔ
frontV = i e ɛ
centV = ə a
backV = u o ɔ

P  = m p  b  ɸ  β
T  = n t  d  θ  ð
Tt = n t  d  s  z ð
Tj = ɲ tɕ dʑ ɕ  ʑ ʑ
Kj = ɲ tɕ dʑ ɕ  ʑ
Kk = ŋ k  g  sk ɣ
K  = ŋ k  g  x  ɣ

// if these included t and d, then tɕ would expand to tɕɕ, which we don't want
KT  = ŋ k  g  sk ɣ n s z ð
KTj = ɲ tɕ dʑ ɕ  ʑ ɲ ɕ ʑ ʑ

R = w r l j
S = s z
H = h ɦ ʔ
N = m n ɲ ŋ

C = {P} {T} {Kj} {K} {R} {S} {H}

// Consonants are easier, so let's do them first
// unconditional β/w merger
β > w
// loss of /ɣ/ -- it becomes /g/ initially and when geminate, and disappears
// elsewhere
ɣ > g / #_
ɣɣ > g
ɣ > 0

// Partial merger of /ŋ/ and /g/. They still contrast intervocalically, and
// after voiced consonants
// Word initially, /ŋ/ becomes [g]
ŋ > g / #_
// before velars and nasals, and word finally, /g/ becomes [ŋ]
g > ŋ / {V}_{K}|{N}|#

// Palatalization of velars: ŋ k g sk ɣ > ɲ tɕ dʑ ɕ ʑ
// before front vowels and palatals
// and of dentals/alveolars before palatals
// velars and alveolars before /j/ (/j/ is lost in this case)
{0:Kk}j > {0:Kj}
{0:Tt}j > {0:Tj}
// velars before front vowels
{0:Kk} > {0:Kj} / _ˈ?{frontV}
// velars and alveolars before palatals
{0:KT} > {0:KTj} / _{KT}*{Kj}

// clean up geminate platals
ɕ > 0 / t_t
ʑ > 0 / d_d

// Spirantization of plossive + h clusters
vdPlos = b d g
vlPlos = p t k
vdFric = β ð ɣ
vlFric = ɸ θ x
{0:vlPlos}h > {0:vlFric}

// Loss of glottals
{H} > 0

// Assimilation of liquid clusters
rl = r l
{rl} > {0:rl} / _{0:rl}

// Now vowels...
// Vowels in hiatus:
// both unstressed or first one stressed: second one becomes a glide or
// lengthens preceding vowel (or disappears if it's already long)
{frontV} > ː / {frontV}_ ! _ː
{frontV} > 0 / {frontV}ː_ ! _ː
{backV} > ː / {backV}_ ! _ː
{backV} > 0 / {backV}ː_ ! _ː
{frontV} > j / {V}ː?_ ! _ː
{centV} > ː / {V}_ ! _ː
{centV} > 0 / {V}ː_ ! _ː
{backV} > w / {V}ː?_ ! _ː
// second one stressed: first one becomes a glide (except central vowels, which
// disappear)
{frontV} > j / _ˈ{V}
{backV} > w / _ˈ{V}
{centV} > 0 / _ˈ{V}

// Loss of short unstressed final vowels (this should happen before open/closed
// things -- I want to lose phonemes, and not immediately rephonemicize them)
// This should only happen when it wouldn't create an invalid coda -- allow:
// {C}
// ({N}|{R})?({plos}|{fric})
// ({N}|{R})?{plos}{fric}
// ({N}|{R})?{fric}{plos}
plos = {vdPlos} {vlPlos}
fric = {vdFric} {vlFric} z ʑ s ɕ
nonSon = {plos} {fric}
sonorant = {N} {R}
{V} > 0 / {V}ː?_#
{V} > 0 / {V}ː?{C}_#
{V} > 0 / {V}ː?{sonorant}?{nonSon}_#
{V} > 0 / {V}ː?{sonorant}?{plos}{fric}_#
{V} > 0 / {V}ː?{sonorant}?{fric}{plos}_#

// First, how to define open/closed syllables?
// An onset can be:
// {C}
// {nonSon}{sonorant}
// {S}{C}
// {S}{nonSon}{R}
// 0
// So, now, insert syllable boundaries
{0:S}{1:nonSon}{2:R} > %{0:S}{1:nonSon}{2:R} / _ˈ?{V}
{0:S}{1:C} > %{0:S}{1:C} / _ˈ?{V} ! %{C}*_
{1:nonSon}{2:sonorant} > %{1:nonSon}{2:sonorant} / _ˈ?{V} ! %{C}*_
{1:C} > %{1:C} / _ˈ?{V} ! %{C}*_
0 > % / _#
0 > % / {V}ː?_ˈ?{V}

// stressed vowels
// stressed long vowels:
// a series of changes occur:
// first, vowel breaking
eː > ej / ˈ_
ɛː > ea / ˈ_
oː > ow / ˈ_
ɔː > oa / ˈ_
// then, some shifts:
ej > iː
ow > aw
ˈoa > wˈa
// and more breaking
iː > aj / ˈ_
uː > aw / ˈ_
// at this point, the only remaining stressed long monophthong is /ˈaː/ < *ahə

// stressed short vowels mostly stay the same

// unstressed short vowels:
// in closed syllables,
// /i e/ merge to [i], /u o/ merge to [u], /ə a/ merge to [a]
usc = i i ɛ a a u u ɔ
{0:V} > {0:usc} / _{C}+% ! ˈ_
// in open syllables,
// /e ɛ/ merge to [e], /o ɔ/ merge to [e], /ə a/ merge to [ə]
uso = i e e ə ə u o o
{0:V} > {0:uso} / _% ! ˈ_

// unstressed long vowels:
// in closed syllables,
// /eː ɛː/ merge to [ɛ], [oː ɔː] merge to [ɔ], /əː aː/ merge to [a], and all other vowels shorten
ulc = i ɛ ɛ a a u ɔ ɔ
{0:V}ː > {0:ulc} / _{C}+% ! ˈ_

// in open syllables,
// /eː ɛː/ merge to [e], /oː ɔː/ merge to [o], /əː aː/ merge to [ə] and all other vowels shorten
ulo = i e e ə ə u o o
{0:V}ː > {0:ulo} / _% ! ˈ_

// get rid of syllable markers
% > 0

// get rid of geminate glides
j > 0 / j_
w > 0 / w_
